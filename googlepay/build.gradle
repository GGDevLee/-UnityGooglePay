plugins {
    id 'com.android.library'
    id 'maven-publish'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    // 分类器 生成api文档jar
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

afterEvaluate { project ->
    tasks.all { task ->
        if (task.name.equalsIgnoreCase('publishGeneratePublicationToMavenRepository')) {
            // task.dependsOn 这里表示 后面的任务执行完成以后才执行当前任务（publishGeneratePublicationToMavenRepository）
            task.dependsOn tasks.getByName('assemble')
        }
    }
}

// 名称
def pom_artifact_id = "googlepay"
// 分组
def group_id = "com.nightgame.googlepay"
// 本地仓库
def local_repo_url = layout.buildDirectory.dir('../repo')
// 版本号
def version_name = "1.0.0"

afterEvaluate {
    publishing {
        publications {
            // pushlish任务名字 Generate
            // 可以配置多个任务 这里配置了一个
            Generate(MavenPublication) {
                groupId = group_id
                artifactId = pom_artifact_id
                version = version_name

                artifact("$buildDir/outputs/aar/${pom_artifact_id}-release.aar")
            }
        }

        repositories {
            maven {
                url local_repo_url
            }
        }
    }
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        warningsAsErrors true
        checkReleaseBuilds false
        abortOnError true
    }
}

dependencies {
    implementation 'com.nightgame.leeframework:leeframework:1.0.1'
    implementation "com.android.billingclient:billing:5.0.0"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task publishToRepo(dependsOn: ['build', 'publishGeneratePublicationToMavenRepository']) {
    group = group_id
}